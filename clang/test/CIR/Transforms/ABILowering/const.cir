// RUN: cir-opt --cir-abi-lowering -o %t.cir %s
// RUN: FileCheck --input-file %t.cir %s

!s32i = !cir.int<s, 32>
!S = !cir.record<struct "S" {!s32i, !s32i, !s32i}>
!Field = !cir.data_member<!s32i in !S>
!Method = !cir.method<!cir.func<(!s32i)> in !S>

module attributes {
  cir.triple = "x86_64-unknown-linux-gnu",
  dlti.dl_spec = #dlti.dl_spec<i128 = dense<128> : vector<2xi64>, f80 = dense<128> : vector<2xi64>, !llvm.ptr<271> = dense<32> : vector<4xi64>, !llvm.ptr<272> = dense<64> : vector<4xi64>, i64 = dense<64> : vector<2xi64>, f16 = dense<16> : vector<2xi64>, i32 = dense<32> : vector<2xi64>, f128 = dense<128> : vector<2xi64>, !llvm.ptr<270> = dense<32> : vector<4xi64>, f64 = dense<64> : vector<2xi64>, !llvm.ptr = dense<64> : vector<4xi64>, i1 = dense<8> : vector<2xi64>, i8 = dense<8> : vector<2xi64>, i16 = dense<16> : vector<2xi64>, "dlti.stack_alignment" = 128 : i64, "dlti.endianness" = "little">
} {
  cir.func @const_data_member() -> !Field {
    %0 = cir.const #cir.data_member<1> : !Field
    cir.return %0 : !Field
  }
  // CHECK:      @const_data_member() -> !s64i
  // CHECK-NEXT:   %[[RES:.+]] = cir.const #cir.int<4> : !s64i
  // CHECK-NEXT:   cir.return %[[RES]] : !s64i
  // CHECK-NEXT: }

  cir.func private @f(%arg0: !cir.ptr<!S>, %arg1: !s32i)
  cir.func @const_method_nonvirtual() -> !Method {
    %0 = cir.const #cir.method<@f> : !Method
    cir.return %0 : !Method
  }
  // CHECK:      @const_method_nonvirtual() -> ![[ABI_TY:.+]] {
  // CHECK-NEXT:   %[[RES:.+]] = cir.const #cir.const_record<{#cir.global_view<@f> : !s64i, #cir.int<0> : !s64i}> : ![[ABI_TY]]
  // CHECK-NEXT:   cir.return %[[RES]] : ![[ABI_TY]]
  // CHECK-NEXT: }

  cir.func @const_method_virtual() -> !Method {
    %0 = cir.const #cir.method<vtable_offset = 8> : !Method
    cir.return %0 : !Method
  }
  // CHECK:      cir.func @const_method_virtual() -> ![[ABI_TY:.+]] {
  // CHECK-NEXT:   %[[RES:.+]] = cir.const #cir.const_record<{#cir.int<9> : !s64i, #cir.int<0> : !s64i}> : ![[ABI_TY]]
  // CHECK-NEXT:   cir.return %[[RES]] : ![[ABI_TY]]
  // CHECK-NEXT: }
}
