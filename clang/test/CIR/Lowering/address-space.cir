// RUN: cir-translate %s -cir-to-llvmir --disable-cc-lowering -o %t.ll
// RUN: FileCheck --input-file=%t.ll %s -check-prefix=LLVM

!s32i = !cir.int<s, 32>

module attributes {
  cir.triple = "spirv64-unknown-unknown",
  dlti.dl_spec = #dlti.dl_spec<i16 = dense<16> : vector<2xi64>, i32 = dense<32> : vector<2xi64>, i1 = dense<8> : vector<2xi64>, i8 = dense<8> : vector<2xi64>, f128 = dense<128> : vector<2xi64>, f64 = dense<64> : vector<2xi64>, f16 = dense<16> : vector<2xi64>, i64 = dense<64> : vector<2xi64>, !llvm.ptr = dense<64> : vector<4xi64>, "dlti.endianness" = "little", "dlti.global_memory_space" = 1 : ui64>
} {
  cir.global external addrspace(offload_global) @addrspace1 = #cir.int<1> : !s32i
  // LLVM: @addrspace1 = addrspace(1) global i32

  cir.global "private" internal addrspace(offload_local) @addrspace2 : !s32i
  // LLVM: @addrspace2 = internal addrspace(3) global i32 undef

  cir.global external addrspace(target<7>) @addrspace3 = #cir.int<3> : !s32i
  // LLVM: @addrspace3 = addrspace(7) global i32

  // LLVM: define void @foo(ptr %0)
  cir.func @foo(%arg0: !cir.ptr<!s32i>) {
    // LLVM-NEXT: alloca ptr,
    %0 = cir.alloca !cir.ptr<!s32i>, !cir.ptr<!cir.ptr<!s32i>>, ["arg", init] {alignment = 8 : i64}
    cir.return
  }

  // LLVM: define void @bar(ptr addrspace(1) %0)
  cir.func @bar(%arg0: !cir.ptr<!s32i, addrspace(target<1>)>) {
    // LLVM-NEXT: alloca ptr addrspace(1)
    %0 = cir.alloca !cir.ptr<!s32i, addrspace(target<1>)>, !cir.ptr<!cir.ptr<!s32i, addrspace(target<1>)>>, ["arg", init] {alignment = 8 : i64}
    cir.return
  }

  // LLVM: define void @baz(ptr %0)
  cir.func @baz(%arg0: !cir.ptr<!s32i, addrspace(target<0>)>) {
    // LLVM-NEXT: alloca ptr,
    %0 = cir.alloca !cir.ptr<!s32i, addrspace(target<0>)>, !cir.ptr<!cir.ptr<!s32i, addrspace(target<0>)>>, ["arg", init] {alignment = 8 : i64}
    cir.return
  }

  // LLVM: define void @test_lower_offload_as()
  cir.func @test_lower_offload_as() {
    %0 = cir.alloca !cir.ptr<!s32i, addrspace(offload_private)>, !cir.ptr<!cir.ptr<!s32i, addrspace(offload_private)>>, ["arg0", init] {alignment = 8 : i64}
    // LLVM-NEXT: alloca ptr,
    %1 = cir.alloca !cir.ptr<!s32i, addrspace(offload_global)>, !cir.ptr<!cir.ptr<!s32i, addrspace(offload_global)>>, ["arg1", init] {alignment = 8 : i64}
    // LLVM-NEXT: alloca ptr addrspace(1),
    %2 = cir.alloca !cir.ptr<!s32i, addrspace(offload_constant)>, !cir.ptr<!cir.ptr<!s32i, addrspace(offload_constant)>>, ["arg2", init] {alignment = 8 : i64}
    // LLVM-NEXT: alloca ptr addrspace(2),
    %3 = cir.alloca !cir.ptr<!s32i, addrspace(offload_local)>, !cir.ptr<!cir.ptr<!s32i, addrspace(offload_local)>>, ["arg3", init] {alignment = 8 : i64}
    // LLVM-NEXT: alloca ptr addrspace(3),
    %4 = cir.alloca !cir.ptr<!s32i, addrspace(offload_generic)>, !cir.ptr<!cir.ptr<!s32i, addrspace(offload_generic)>>, ["arg4", init] {alignment = 8 : i64}
    // LLVM-NEXT: alloca ptr addrspace(4),
    cir.return
  }
}
